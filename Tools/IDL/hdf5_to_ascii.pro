;+
;
; NAME:       HDF5_TO_ASCII
;
; AUTHOR:     A. Mignone
;
; PURPOSE:    Convert a 1D hdf5 dataset generated with PLUTO to
;             ASCII format (multicolumn)
;
; SYNTAX:     HDF5_TO_ASCII, nout[,level=level][,filename=filename]
;
; ARUGMENTS:
;
;       nout:    the output file number
;
; KEYWRODS:
;
;      level:    an integer number giving the maximum refinement level
;                to be read
;      filename: the output filename. Default is "hdf5_data.tab"
;
; LAST MODIFIED:
;
;   March 24, 2015 by A. Mignone (mignone@ph.unito.it)
;
;-
PRO HDF5_TO_ASCII,nout,dir=dir,level=level,filename=filename

  COMMON PLUTO_GRID
  COMMON PLUTO_VAR
  COMMON PLUTO_RUN
  
; -- Set keywords --

  IF (NOT KEYWORD_SET(dir))      THEN dir = "./"
  IF (NOT KEYWORD_SET(level))    THEN level = 0
  IF (NOT KEYWORD_SET(filename)) THEN filename = "hdf5_data.tab"

; -- Load datafile --

  PLOAD,/SILENT,/HDF5,level=0,dir=dir,nout
  dx0 = dx1[0] 
  PLOAD,/SILENT,/HDF5,level=level,dir=dir,nout

; -- Open datafile --

  OPENW, fp, filename, /GET_LUN, WIDTH = 512

; -- Write Header -- 

  PRINTF,fp,"# //////////////////////////////////////////////////////////////"
  PRINTF,fp,"# Generated by HDF5_TO_ASCII on ",SYSTIME()
  PRINTF,fp,"# "
  PRINTF,fp,"# Max level: ",ARG2STR(level)
  PRINTF,fp,"# Npoints:   ",ARG2STR(N_ELEMENTS(x1))
  PRINTF,fp,"# time:      ",ARG2STR(t)
  
  legend = [" x1 "," lev ", " rho "]
  IF (n_elements(vx1) GT 0) THEN legend = [legend, " vx1 "]
  IF (n_elements(vx2) GT 0) THEN legend = [legend, " vx2 "]
  IF (n_elements(vx3) GT 0) THEN legend = [legend, " vx3 "]

  IF (n_elements(bx1) GT 0) THEN legend = [legend, " Bx1 "]
  IF (n_elements(bx2) GT 0) THEN legend = [legend, " Bx2 "]
  IF (n_elements(bx3) GT 0) THEN legend = [legend, " Bx3 "]

  IF (n_elements(prs)  GT 0) THEN legend = [legend, " prs "]
  IF (n_elements(tr1)  GT 0) THEN legend = [legend, " tr1 "]
  IF (n_elements(x_HI)  GT 0) THEN legend = [legend, " x_HI "]
  IF (n_elements(x_HII) GT 0) THEN legend = [legend, " x_HII "]
  IF (n_elements(x_H2)  GT 0) THEN legend = [legend, " x_H2 "]
  
  PRINTF,fp,"# Columns: ",legend
  PRINTF,fp,"# //////////////////////////////////////////////////////////////"
  
; -- Write data values --

  FOR n = 0, N_ELEMENTS(x1)-1 DO BEGIN
  
  ; -- Form datafile --
   
    jmp  = 0 + ALOG10(dx0/dx1[n])/ALOG10(2.0)
    data = [x1[n], ROUND(jmp), rho[n]]
    IF (n_elements(vx1) GT 0) THEN data = [data, vx1[n]]
    IF (n_elements(vx2) GT 0) THEN data = [data, vx2[n]]
    IF (n_elements(vx3) GT 0) THEN data = [data, vx3[n]]
    IF (n_elements(bx1) GT 0) THEN data = [data, bx1[n]]
    IF (n_elements(bx2) GT 0) THEN data = [data, bx2[n]]
    IF (n_elements(bx3) GT 0) THEN data = [data, bx3[n]]
    IF (n_elements(prs) GT 0) THEN data = [data, prs[n]]
    IF (n_elements(tr1) GT 0) THEN data = [data, tr1[n]]
    IF (n_elements(x_HI) GT 0)  THEN data = [data, x_HI[n]]
    IF (n_elements(x_HII) GT 0) THEN data = [data, x_HII[n]]
    IF (n_elements(x_H2) GT 0)  THEN data = [data, x_H2[n]]

    ncol = N_ELEMENTS(data)
    PRINTF,fp, data, FORMAT='(32E15.6)'
  ENDFOR
  CLOSE,fp
END

