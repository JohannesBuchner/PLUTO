C      _______              __
C     / ___/ /  ___  __ _  / /  ___
C    / /__/ _ \/ _ \/  V \/ _ \/ _ \
C    \___/_//_/\___/_/_/_/_.__/\___/
C    Please refer to Copyright.txt, in Chombo's root directory.

#include "CONSTANTS.H"

        subroutine SETDIRICHLETFACEFLUX(
     &    CHF_FRA1[faceFlux],
     &    CHF_CONST_FRA1[phi],
     &    CHF_CONST_REAL[value],
     &    CHF_CONST_REALVECT[dx],
     &    CHF_CONST_INT[idir],
     &    CHF_CONST_INT[iside],
     &    CHF_BOX[box])

        integer CHF_DDECL[i;j;k]

        real_t ihdx

        ihdx = two/dx(idir)

        CHF_MULTIDO[box;i;j;k]
          faceFlux(CHF_IX[i;j;k]) = iside * ihdx * (phi(CHF_IX[i;j;k]) - value)
        CHF_ENDDO

        return
        end

        subroutine SETHODIRICHLETFACEFLUX(
     &    CHF_FRA1[faceFlux],
     &    CHF_CONST_FRA1[phi],
     &    CHF_CONST_REAL[value],
     &    CHF_CONST_REALVECT[dx],
     &    CHF_CONST_INT[idir],
     &    CHF_CONST_INT[iside],
     &    CHF_BOX[box])

        integer CHF_DDECL[i;j;k], chf_ddecl[ioff;joff;koff]

        real_t dxinv

        chf_dterm[ioff = iside*chf_id(0,idir);
                  joff = iside*chf_id(1,idir);
                  koff = iside*chf_id(2,idir)]

        dxinv = one/dx(idir)

        CHF_MULTIDO[box;i;j;k]
          faceFlux(CHF_IX[i;j;k]) =
     &       iside * dxinv * (three*phi(CHF_IX[i;j;k])
     &                  - one/three*phi(CHF_IX[i+ioff;j+joff;k+koff])
     &                  - eight/three*value)
        CHF_ENDDO

        return
        end
